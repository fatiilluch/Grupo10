import Personaje.*
import HechizoDeLogo.*
import hechizoBasico.*
import ArmasFilosas.*
import CollarDivino.*
import ArmaduraGeneral.*
import armadura.*
import espejo.*
import libroDeHechizos.*
import Comerciante.*
import refuerzos.*

describe "Tests Punto 4 de la Entrega 2" 
{
	var alacachulaCachicomula = new HechizoDeLogo(nombre = "alacachula cachicomula", multiplicador = 1)
	var xenia = new Personaje(hechizoPreferido = alacachulaCachicomula, capacidadMaximaDeCarga = 200)
	var thor = new Personaje(hechizoPreferido = hechizoBasico, capacidadMaximaDeCarga = 200)
	var loki = new Personaje(hechizoPreferido = hechizoBasico, monedas = 5, capacidadMaximaDeCarga = 200)
	var mockHnos = new Comerciante(comerciante = new Independiente(comision = 0))
	var collarDivino = new CollarDivino(perlas = 5, peso = 0, fechaCompra = new Date())
		
	test "El precio de lista de un hechizo de logo cuyo nombre es alacachula cachicomula es 22" 
	{
		assert.equals(alacachulaCachicomula.precioDeLista(),22)
	}
	
	test "Si Xenia canjea el hechizo por un hechizo básico, le quedan 100 monedas"
	{
		xenia.canjeaPor(hechizoBasico)
		assert.equals(xenia.monedas(),100)
	}
	
	test "Si thor canjea el hechizo por el hechizo alacachula cachicomula del juego de datos, le quedan 83 monedas"
	{
		thor.canjeaPor(alacachulaCachicomula)
		assert.equals(thor.monedas(),83)
	} 
	
	test "thor canjea 2 veces el mismo hechizo basico. Queda con 90 monedas" 
	{
		thor.canjeaPor(hechizoBasico)
		thor.canjeaPor(hechizoBasico)
		assert.equals(thor.monedas(),90)    	
	} 
	
	test"Loki no puede canjear el hechizo por el de alacachula cachicomula del juego de datos ya que no dispone de suficientes monedas"
	{
		const mensajeDeError = "No se puede adquirir este articulo"
		
		const excepcionEsperada = new ExcepcionPorFaltaDeArticulo (mensajeDeError)
		
		const bloqueQueDebeExplotar = 
		{
			loki.canjeaPor(alacachulaCachicomula)	
		}
		assert.throwsExceptionLike(excepcionEsperada, bloqueQueDebeExplotar)
	}
	
	test "Si Xenia compra a Mock Hnos una espada, un collar divino (de 5 perlas), una armadura sin refuerzo y otra armadura con malla de cota cuyo valor de lucha es 5. A xenia le quedan 85.5 monedas."
	{
		var espada = new ArmaFilosa(peso = 0, fechaCompra = new Date()) 
		var armaduraSinRefuerzo = new ArmaduraGeneral(refuerzo = ninguno, peso = 0, fechaCompra = new Date())
		var armaduraConMalla = new ArmaduraGeneral (refuerzo = new CotaMalla(poder = 5), peso = 0, fechaCompra = new Date())
		xenia.compra(espada, mockHnos)
		xenia.compra(collarDivino, mockHnos)
		xenia.compra(armaduraSinRefuerzo, mockHnos)
		xenia.compra(armaduraConMalla, mockHnos)
		assert.equals(xenia.monedas(),85.5)	
	}
	
	test "Xenia compra a Mock Hnos una espada, un collar divino (de 5 perlas), una armadura sin refuerzo y otra armadura con malla de cota cuyo valor de lucha es 5. xenia tiene 4 artefactos"
	{
		var espada = new ArmaFilosa(peso = 0, fechaCompra = new Date())
		var armaduraSinRefuerzo = new ArmaduraGeneral(refuerzo = ninguno, peso = 0, fechaCompra = new Date())
		var armaduraConMalla = new ArmaduraGeneral (refuerzo = new CotaMalla(poder = 5), peso = 0, fechaCompra = new Date()) 
		xenia.compra(espada, mockHnos)
		xenia.compra(collarDivino, mockHnos)
		xenia.compra(armaduraSinRefuerzo, mockHnos)
		xenia.compra(armaduraConMalla, mockHnos)
		assert.equals(xenia.artefactos().size(),4)
	}
	
	test "Thor compra una armadura con  bendición como refuerzo, y una armadura reforzada con el hechizo alacachula. A Thor le quedan 74 monedas"
	{
		var armaduraConBendicion = new ArmaduraGeneral(refuerzo = bendicion, peso = 0, fechaCompra =  new Date())
		var armaduraConAlacachula = new ArmaduraGeneral(refuerzo = alacachulaCachicomula, peso = 0, fechaCompra =  new Date())
		thor.compra(armaduraConBendicion, mockHnos)
		thor.compra(armaduraConAlacachula, mockHnos)
		assert.equals(thor.monedas(),74) 
	}
	
	test "Loki quiere comprar el espejo,pero como tiene solo 5 monedas no puede"
	{
		const pepe = new Comerciante()
		const mensajeDeError = "No se puede adquirir este articulo"
		
		const excepcionEsperada = new ExcepcionPorFaltaDeArticulo (mensajeDeError)
		
		const bloqueQueDebeExplotar =
		{
			loki.compra(espejo,pepe)
		} 
		assert.throwsExceptionLike(excepcionEsperada, bloqueQueDebeExplotar)
	}

	
	test "xenia canjea un libro de hechizos que contiene el hechizo de logos de nombre alacachula cachicomula y el hechizo basico. Le quedan 69 monedas."
	{
		libroDeHechizos.agregaHechizo(alacachulaCachicomula)
		libroDeHechizos.agregaHechizo(hechizoBasico)
		xenia.canjeaPor(libroDeHechizos)
		assert.equals(xenia.monedas(),69)
	}
}
